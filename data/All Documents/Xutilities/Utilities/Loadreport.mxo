...............................................................................
. Procedure to load reports into Reporter
.	INPUTS (Prompted via HTML Form)
.	1. Directory containing .mxr files - Will prompt for the directory 
.	containing the .mxr files (default is _datadir\reports)
.	2. Optional Report Name Prefix - if entered will prefix report names
.	with entered text (useful if you want to clone reports under a different
.	name.
.	3. Optional Username - enter a username if you want to add the reports
.	for a user other than the currently logged in user. Will default to
.	the current user.
.
.
.	The report files must be complete and include the PROCEDURE reportscript
.	header line and a RETURN statement at the end (same format as generated
.	in the DSnnnnnnnnnn.mxo files generated by gui CREATE REPORT)
.
.	The report files must have an extension of .mxr. This program will add
.	all files in the specified directory path with a .mxr extension
.
.	Report name will be same as filename excluding the .mxr extension
.	Note: If the user already has a report of that name it will be
.	overwritten.
.
.	Author: Greg Parsons
.	Date: 20th June 2010
.	Updates:
.	20/6/2010	Original Version
.	.
.
...............................................................................
PROCEDURE Loadreport
default dirpath=''
default prefix=''
default username=''
default action='0'
API_START
...	Check if form already submitted 0=No, 1=yes
action=getformtext('action','0')
SWITCH
...
... 	If form not submitted yet need to output form requesting the variables
...
CASE action eq '0'
	dirpath=_datadir
	ADDTEXT dirpath '\reports'

line=concat(#_homedir,'/maxiplandata/All Documents/Xutilities/Utilities/Loadreport')
	STARTFORM NAME 'Loadreport' to #line
		COPYHTML <input type="hidden" name="action" value="1">
		COPYHTML Directory containing reports: 
		STREAMHTML <input name="dirpath" size="60" value="
		STREAMHTML #dirpath
		COPYHTML "> </BR>
		COPYHTML Optional Prefix for Report Names:       
		COPYHTML <input name="prefix" size="3" value=""></br>
		COPYHTML User Id to use (default is current user): 
		STREAMHTML <input name="uname" size="20" value="
		STREAMHTML #_userName
		COPYHTML "></br>
		COPYHTML <input type="submit" value="SUBMIT" name="SubmitCompanyScreen">
	ENDFORM
...
...	if form submitted add the reports in the specified directory path
...	to the specified user, with the specified report name prefix
...
CASE action eq '1'
	dirpath=getformtext('dirpath','')
	prefix=getformtext('prefix','')
	uname=getformtext('uname','')

	directory #dirpath
	dosdir '*.mxr'
	nritems=itemsinfo('_dosfile','items')
	flist=_dosfile
	WHEN uname eq ''
		uname = _userName
	ENDWHEN
	WHEN nritems GT 0
		. loop through the list of files and publish them with changed name as necessary
		FOR j,1,nritems,1
			fname=dirpath
			rep=flist[j]
			addtext fname '\'
			addtext fname rep
			flen=sizeof(rep)
			namelen=flen-4
			repname=SUBSTR(rep,1,namelen)
			prepname = prefix
			ADDTEXT prepname repname
			api_publish
				with option = 'importscript'
				with sourceFile = fname
				with reportName = prepname
				with userLogin = uname
			mesg='Report "'
			ADDTEXT mesg prepname,'" added from file "',rep,'" for user ',uname
			COPYHTML #mesg
			COPYHTML </br>
		ENDFOR
	ENDWHEN	
ENDSWITCH
API_END
RETURN
